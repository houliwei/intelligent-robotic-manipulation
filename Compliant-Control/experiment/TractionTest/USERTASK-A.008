' TCP/IP  Server
' -----------------------------------------------------------
' 功能：与labview上位机连接的调试
' -----------------------------------------------------------
' 测试流程：
' 1. 机器人开放端口，等待labview接入。
' 2. 接入后，机器人向labview发送成功连接的命令“connected”。
' 3. labview向机器人发送请求命令“1”。
' 4. 机器人发送给labview一个数值“3.14”。
' -----------------------------------------------------------


' -----------------------------------------------------------
*START
' -----------------------------------------------------------
	FOR L1% = 1 TO 20 STEP 1	'初始化1-20#变量。 1：状态记录  2：接收  3：发送
		V$[L1%] = ""
		V![L1%] = 0
		V%[L1%] = 0
	NEXT


' -----------------------------------------------------------
*CREATE	
' -----------------------------------------------------------
	V1$ = "sockcreat"
		SOCKCREATE 1,0    ' 1：套接字编号  0: TCP（0代表TCP，1代表UDP）
	IF E1%<0 Then *RESET



' -----------------------------------------------------------
*SOCKBIND	                   '分配端口
' -----------------------------------------------------------
	V1$ = "sockbind"
		SOCKBIND 1,2000  ' 1：套接字编号   2000：端口编号
	IF E1%<0 Then *RESET



' -----------------------------------------------------------	
*SOCKWAIT	                   '等待客户端连接
' -----------------------------------------------------------
	V1$ = "sockwait"
		SOCKWAIT 1,2,0    ' 1：等待连接套接字编号  2：通信套接字编号  0：持续等待连接，没有超时限制   
	IF E1%<0 Then *RESET


	
' -----------------------------------------------------------
*Connected
' -----------------------------------------------------------
	V1$ = "Connect_OK"
	'V3$ = "Connected"
	'SETSTR 2,V3$,0,LEN(V2$)   '将存储于 V2$ 中字符串的开头 3 字节保有至 2 号缓冲区的开头位置(0)。
	'SOCKSENDSTR 2,V3$,LEN(V3$),5,V100%   	' 将V3$发送至使用 2 号套接字连接的通信设备。
												' 实际发送的数据大小写入 V20%。
												' 5秒以内没有发送时，超时。
												' 这里省略了第 6 参数，省略时等同于指定为 0。
												' (0:不追加终止字符;1 : 字符串末尾追加“\r”后发送;2 : 字符串末尾追加“\n”后发送;3 : 字符串末尾追加“\r\n”后发送) 



' -------------------send force to labview
*SOCKSEND   
' -----------------------------------------------------------
	
	
	V1$ = "send"
	V15$ = STR$(SYSTEM!(1500))
	V16$ = STR$(SYSTEM!(1501))
	V17$ = STR$(SYSTEM!(1502))
	V3$ = V15$+","+V16$+","+V17$
	SOCKSENDSTR 2,V3$,LEN(V3$),5,V100%
	
	
	
	'V2! = 3
	'SETREAL 2,V2!,0   '将存储于实数变量 V2!的值保存到 2 号缓冲区开头。(从指定的位置写入 4 字节的数据。)
	'SOCKSEND 2,2,4,0,V19%  '以最大4字节讲2号缓冲区保存的数据发送至通过 2 号套接字连接的通信设备。
	                       'SOCKSEND在数据发送后，将实际发送的数据大小写入整数变量 V19%，完成步骤。
						   '5 秒以内没有发送时，超时。


' -----------------------------------------------------------		
*SOCKRECV
' -----------------------------------------------------------

	V1$ = "SOCKRECV"

    L1% = 0
	L1$ = ""
	L2$ = ""
	L3$ = ""
	L4$ = ""
	
	V7% = 0    ' robot wait for communication
		
	SOCKRECV 2,1,0,5,V20%    '2:套接字编号 1：缓冲区编号 0：接收数据长度不限制（以字节为单位） 5：超时时间  v20%：将实际接收的数据大小写入 V20%
	GETSTR 1,V2$,0,V20%    '将存储于 1 号缓冲区数据开头V20%字节复制到字符串变量 V2$。
	L2% = LEN(V2$)
		
*FINDfirstcomma
	L1% = L1% + 1
	L2$ = L2$+ L1$
	L1$ = MID$(V2$,L1%,1)
	IF L1$<>"," THEN *FINDfirstcomma
	L1$ = ""
	
*FINDsecondcomma
	L1% = L1% + 1
	L3$ = L3$+ L1$
	L1$ = MID$(V2$,L1%,1)
	IF L1$<>"," THEN *FINDsecondcomma
	L1$ = ""
	
*TheLastOne
	L1% = L1% + 1
	L4$ = L4$+ L1$
	L1$ = MID$(V2$,L1%,1)
	IF L1%<>L2% THEN *TheLastOne
	L4$ = L4$+ L1$
	
	V9! = VAL(L2$)   
	V10! = VAL(L3$) 
	V11! = VAL(L4$)
	
	V7% = 1
	PAUSE 50
	
	GOTO *SOCKSEND	


' -----------------------------------------------------------
*Closesock
' -----------------------------------------------------------
    SOCKCLOSE 1    ' 关闭1号套接字
    SOCKCLOSE 2	   ' 关闭2号套接字
    EXIT



' -----------------------------------------------------------
*RESET
' -----------------------------------------------------------
    V1$ = "套接字复位"	'套接字复位
	PRINT #0,STR$(E1%)
	PRINT #0,STR$(E2%)
	SOCKCLOSE 1
	SOCKCLOSE 2
	PAUSE 3000
	GOTO *START	



' -----------------------------------------------------------
*EXIT
' -----------------------------------------------------------
	EXIT






